package com.ftn.sbnz.rules.filtering;

import com.ftn.sbnz.model.dtos.TravelPreferences;
import com.ftn.sbnz.model.models.Location;
import com.ftn.sbnz.model.enums.Transport;
import com.ftn.sbnz.model.enums.PublicTransportAccessibility;
import com.ftn.sbnz.model.models.RuleParameter;
import com.ftn.sbnz.model.enums.Budget;
import com.ftn.sbnz.model.events.RoadStatusEvent;
import com.ftn.sbnz.model.enums.RoadStatus;

query "getLocations"
    $location: Location()
end

rule "Filter out locations not accessible by public transport"
    agenda-group "scoring"
    salience 20
    when
        TravelPreferences(
            transport == Transport.PUBLIC_TRANSPORT,
            $mustHave: mustHaveLocationIds
        )
        $location: Location(
            $id: id.toString(),
            publicTransportAccessibility == PublicTransportAccessibility.CAR_ONLY,
            eval($mustHave not contains $id)
        )
    then
        delete($location);
        System.out.println("Filtered location due to transport: " + $location.getName());
end

rule "Filter out locations based on fitness level"
    agenda-group "scoring"
    salience 20
    when
        $prefs: TravelPreferences(
            $fitness: fitnessLevel,
            $mustHave: mustHaveLocationIds
        )
        $location: Location(
            $id: id.toString(),
            $reqFitness: requiredFitness,
            eval($reqFitness.ordinal() > $fitness.ordinal()),
            eval($mustHave not contains $id)
        )
    then
        delete($location);
        System.out.println("Filtered location due to fitness level: " + $location.getName());
end


rule "Filter out seasonally closed locations"
    agenda-group "scoring"
    salience 20
    when
        $prefs: TravelPreferences(
            $month: travelMonth,
            $mustHave: mustHaveLocationIds
        )
        $location: Location(
            $id: id.toString(),
            seasonal == true,
            (openingMonth > $month || closingMonth < $month),
            eval($mustHave not contains $id)
        )
    then
        delete($location);
        System.out.println("Filtered location due to season: " + $location.getName());
end

rule "Filter locations if access road is closed"
    agenda-group "scoring"
    salience 30
    when
        RoadStatusEvent(roadName == "Vršič Pass", status == RoadStatus.CLOSED)
        $location: Location(name == "Triglav National Park" || name == "Kranjska Gora")
    then
        delete($location);
        System.out.println("Filtered location (due to CEP event) '" + $location.getName() + "' because Vršič Pass is closed.");
end